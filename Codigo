Sub Macro()

    Dim DireccionL As String
    Dim DireccionX As String
    Dim BuscarL As String
    Dim BuscarX As String
    Dim RangoL As Range
    Dim RangoX As Range
    Dim RangosCombinados As Range
    Dim buscarCelda As Range

    ' Buscar la celda que contiene "Variables a graficar"
    Set buscarCelda = Cells.Find(What:="Variables a graficar", LookIn:=xlValues, LookAt:=xlPart, _
                                 SearchOrder:=xlByColumns, SearchDirection:=xlNext, _
                                 MatchCase:=False, SearchFormat:=False)

    ' Salir si no se encuentra la celda
    If buscarCelda Is Nothing Then
        MsgBox "No se encontró la celda 'Variables a graficar'."
        Exit Sub
    End If

    ' Obtener nombres de variables a graficar
    BuscarL = buscarCelda.Offset(1, 0).Value
    BuscarX = buscarCelda.Offset(1, 1).Value

    buscarCelda.CurrentRegion.Cut Destination:=Range("A60")

    Range("A60").Style = "60% - Accent6"
    Range("A60").Font.Size = 20
    Range("A60").Font.Bold = True
    Range("A61:B63").Font.Size = 15
    Cells.EntireColumn.AutoFit
    
    ' Encontrar direcciones de los rangos
    DireccionL = EncontrarDireccion(BuscarL)
    DireccionX = EncontrarDireccion(BuscarX)
    
    ' Establecer los rangos utilizando las direcciones guardadas en las variables
    Set RangoL = Range(DireccionL)
    Set RangoX = Range(DireccionX)

    ' Combinar los dos rangos en uno solo utilizando Union
    Set RangosCombinados = Union(RangoL, RangoX)
    
    ' Crear el gráfico
    Dim miGrafico As ChartObject
    Set miGrafico = ActiveSheet.ChartObjects.Add(Left:=200, Top:=1000, _
    Width:=600, Height:=600)
       
    With miGrafico.Chart
        .SetSourceData Source:=RangosCombinados
        .ChartType = xlBarClustered ' Establecer el tipo de gráfico
        ' Configurar títulos y etiquetas
        .HasTitle = True
        .ChartTitle.Text = BuscarX & " por " & BuscarL
        .ChartTitle.Font.Size = 20
        .ChartTitle.Font.Bold = True
        .Axes(xlCategory).TickLabels.Font.Size = 16
        .Axes(xlValue).TickLabels.Font.Size = 16
        .ChartColor = 12
        .ChartStyle = 220
    End With
    
    ' Configurar títulos y etiquetas
    Range("D60").Value = "Columnas graficadas"
    Range("D60").Style = "60% - Accent6"
    Range("D60").Font.Size = 20
    Range("D60").Font.Bold = True
    
    Range("D61").Value = BuscarX
    Range("D62").Value = BuscarL
    
    Range("D61:D62").Font.Size = 15
    Cells.EntireColumn.AutoFit
    
End Sub
